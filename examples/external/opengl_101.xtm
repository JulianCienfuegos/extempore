;;; opengl_101.xtm -- Simplest possible OpenGL example

;; Author: Andrew Sorensen
;; Keywords: extempore

;;; Commentary:

;;

;;; Code:

(sys:load "libs/external/gl2.xtm")
(sys:load "libs/contrib/glwt.xtm")

(glwt_init_api_version GLWT_PROFILE_COMPATIBILITY 2 1)

(define width 1920.0)
(define height 1080.0)

(define *glwt-window* (glwt_window_create (real->integer width)
                                          (real->integer height)))

(glwt_window_show *glwt-window* 1)
(glwt_make_current *glwt-window* 1)
(glwt_swap_interval *glwt-window* 1)

(bind-func my-gl-loop
  (let ((size 0.01))
    (lambda (degree)
      (glClear (+ GL_COLOR_BUFFER_BIT GL_DEPTH_BUFFER_BIT))
      (glLoadIdentity)
      (glTranslatef 0.0 -1.0 0.0)
      (let ((i 0.0))
	(dotimes (i 1000.0)
	  (glTranslatef (/ i 2000.0) 0.0 0.0)
	  (glColor3f 1.0 (/ i 1500.0) (/ (- 1000. i) 500.0))
	  (glRotatef degree (/ i 200000.0) 0.5 0.0)
	  (glBegin GL_QUADS)
	  (glVertex2f 0.0 0.0)
	  (glVertex2f (* -5. size) 0.0)
	  (glVertex2f size (* 2. size))
	  (glVertex2f 0.0 size)
	  (glEnd))))))

;; standard impromptu callback                                                
(define run-my-opengl-loop
  (lambda (degree)
    (my-gl-loop degree)
    (glwt_swap_buffers *glwt-window*)
    (if (<> (glwt_window_closed *glwt-window*) 1)
        (callback (+ (now) (/ *second* 30)) 'run-my-opengl-loop (+ degree .005))
        (print "Window closed: finishing my-opengl-loop\n"))))

(run-my-opengl-loop 70.0)
